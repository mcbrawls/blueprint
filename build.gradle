//file:noinspection All

plugins {
    id "fabric-loom" version "1.6-SNAPSHOT"
    id "org.jetbrains.kotlin.jvm"
    id "maven-publish"
}

def ENV = System.getenv()

version = mod_version
group = maven_group

base {
    archivesName = mod_id
}

repositories {
    maven {
        name = "Andante"
        url  = "https://maven.andante.dev/releases/"
    }

    maven {
        name = "Nucleoid"
        url  = "https://maven.nucleoid.xyz/"
    }
}

loom.splitEnvironmentSourceSets()

sourceSets {
    test {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }

    testClient {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath

        compileClasspath += client.compileClasspath
        runtimeClasspath += client.runtimeClasspath

        compileClasspath += test.compileClasspath
        runtimeClasspath += test.runtimeClasspath
    }
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$minecraft_version+build.$yarn_build:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"
    modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_kotlin_version"

    include modImplementation ("dev.andante:bubble:$bubble_version")
    include modImplementation ("dev.andante:audience:$audience_version")
    include modImplementation ("dev.andante:codex:$codex_version")

    include modImplementation ("eu.pb4:sgui:$sgui_version")
    include modImplementation ("net.mcbrawls:brawls-sgui:$brawls_sgui_version")

    modImplementation "com.ptsmods:devlogin:$devlogin_version"

    // test
    testImplementation sourceSets.main.output
    testImplementation "org.apache.logging.log4j:log4j-slf4j2-impl:2.21.1"

    testClientImplementation sourceSets.main.output
    testClientImplementation sourceSets.client.output
    testClientImplementation sourceSets.test.output
}

loom {
    runtimeOnlyLog4j = true

    mods {
        mod_id {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    runs {
        authenticatedClient {
            client()
            name("Minecraft Client (Authenticated)")
            programArg("--msa")
        }

        testClient {
            client()
            name("Minecraft Client (Test)")
            source(sourceSets.testClient)
            runDir("runtest")
        }

        testClientAuthenticated {
            inherit(testClient)
            name("Minecraft Client (Test Authenticated)")
            programArg("--msa")
            runDir("runtest")
        }

        testServer {
            server()
            name("Minecraft Server (Test)")
            source(sourceSets.test)
            runDir("runtest")
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = 17
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(sourcesJar)
        }
    }

    repositories {
        // Andante's Maven
        def mavenUsername = ENV["MAVEN_USERNAME_ANDANTE"]
        def mavenPassword = ENV["MAVEN_PASSWORD_ANDANTE"]
        if (mavenUsername && mavenPassword) {
            maven {
                name = "Andante"
                url  = "https://maven.andante.dev/releases/"
                credentials.username = mavenUsername
                credentials.password = mavenPassword
            }
        }
    }
}
